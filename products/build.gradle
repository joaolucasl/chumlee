
buildscript {
    ext {
        grpcVersion = '1.3.0'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.31")
        classpath("com.google.protobuf:protobuf-gradle-plugin:0.8.8")
        classpath("com.github.jengelman.gradle.plugins:shadow:4.0.4")
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'

group = "com.github.joaolucasl.chumlee"
version = "0.0.1"

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.31")
    implementation("org.koin:koin-spark:1.0.1")
    implementation("org.slf4j:slf4j-api:1.7.5")
    implementation("org.slf4j:slf4j-simple:1.7.5")
    implementation("io.github.microutils:kotlin-logging:1.6.24")
    implementation("com.google.code.gson:gson:2.8.5")

    implementation("org.jdbi:jdbi3-core:3.5.1")
    implementation("org.jdbi:jdbi3-kotlin:3.5.1")
    implementation("org.jdbi:jdbi3-kotlin-sqlobject:3.5.1")
    implementation("mysql:mysql-connector-java:8.0.13")

    compile "io.grpc:grpc-protobuf:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"
    compile "io.grpc:grpc-netty:$grpcVersion"
}

def generatedSourcesPath = "$projectDir/src/"

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDir "${generatedSourcesPath}/main/java"
            srcDir "${generatedSourcesPath}/main/grpc"
        }
    }
}

def className = "com.github.joaolucasl.chumlee.products.ServerKt"

protobuf {
    generatedFilesBaseDir = generatedSourcesPath
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

jar {
    manifest {
        attributes["Main-Class"] = className
    }
}

shadowJar {
    baseName = "app"
    classifier = ""
    version = ""
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}